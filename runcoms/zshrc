#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

DIRSTACKSIZE=32
DIRSTACKFILE="${HOME}"/.zdirs

autoload -U is-at-least
# Keep dirstack across logouts
if [[ -f ${DIRSTACKFILE} ]] && [[ ${#dirstack[*]} -eq 0 ]] ; then
    dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
    dirstack=( ${(u)dirstack} )
fi

# Make sure there are no duplicates
typeset -U dirstack

# Share dirstack between multiple zsh instances
function chpwd() {
    if is-at-least 4.1; then # dirs -p needs 4.1
        # Get the dirstack from the file and add it to the current dirstack
        dirstack+=( ${(f)"$(< $DIRSTACKFILE)"} )
        dirstack=( ${(u)dirstack} )
        dirs -pl |sort -u >! ${DIRSTACKFILE}
    fi
}

# By default, ^S freezes terminal output and ^Q resumes it. Disable that so
# that those keys can be used for other things.
unsetopt flowcontrol

# Put any OS-specific config in ~/.zshrc.OS
OSNAME=$(uname -s)
if [[ -r ~/.zshrc.$OSNAME ]]; then
  source ~/.zshrc.$OSNAME
fi

# Put any host-specific config in ~/.zshrc.local
if [[ -s "${ZDOTDIR:-$HOME}/.zshrc.local" ]]; then
  source "${ZDOTDIR:-$HOME}/.zshrc.local"
fi

if [[ $TERM == 'cons25' || $TERM == 'linux' || $ITERM_PROFILE == 'Light' ]]; then
  true  # Console session or 'demo mode', so not starting tmux
else
  if [[ $TMUX != "" ]]; then
    true    # Tmux/screen already running, so not starting a nested session
  else
    if which tmux > /dev/null; then
      if [[ -r ~/.tmux.conf ]]; then
        tmux -f ~/.tmux.conf attach
      else
        # ...otherwise just start a default tmux session
        tmux new -s "$(hostname -s)/$(basename $(pwd))"
      fi
    fi
  fi
fi


[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
