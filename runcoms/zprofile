#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vi'
export VISUAL='vi'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  ~/bin
)

EXTRA_PATHS=(
    ${HOME}/.local/bin
    /usr/local/opt/coreutils/libexec/gnubin
    /Library/Developer/Toolchains/swift-latest.xctoolchain
    ${HOME}/.poetry/bin
    ${HOME}/.cargo/bin
    ${HOME}/.node/bin
    ${GOPATH}/bin
    /usr/local/opt/sqlite/bin
)
for EXTRA_PATH in $EXTRA_PATHS; do
  if [[ -d $EXTRA_PATH ]]; then
  path=(
    $EXTRA_PATH
    $path
  )
  fi
done

path=(
  $path
  /usr/local/{bin,sbin}
  /usr/bin
  /bin
  /usr/sbin
  /sbin
)

fpath=(
  ${HOME}/.zfunc
  $fpath
)

# For some reason `autocd` is no longer set and I'm not sure why
# so reinstating it here
setopt autocd

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

