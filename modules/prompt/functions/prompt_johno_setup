#
# My theme with quite a few bells and whistles:
#   #TODO list main features here
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   John O'Shea <john.m.oshea@gmail.com>
#
# Screenshots:
#   #TODO upload screenshot somewhere
#

function prompt_johno_precmd () {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_steeef_colors[1]}%b%f%u%c${_prompt_steeef_colors[4]}●%f)"
  else
    branch_format="(${_prompt_steeef_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
  set_vpn_status
}


function prompt_johno_setup() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling git-info before each command
  add-zsh-hook precmd prompt_johno_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_steeef_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_steeef_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_steeef_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_steeef_colors[5]}%a%f)"
  local unstaged_format="${_prompt_steeef_colors[2]}●%f"
  local staged_format="${_prompt_steeef_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  if [ "$(whoami)" = "root" ]
  then USER_ID="%K{red}%F{white} %n@%m %f%k"
  else USER_ID="%F{white}%n@%m%f"
  fi

  TIME_DISPLAY="%K{yellow}%F{black} %* %f%k"

  hist_num="%!"
  num_jobs="JOBS:%j"
  shlvl="SHVL:%L"

  PROMPT='
${VPN_PROMPT}%F{blue}[$hist_num $shlvl $num_jobs]%f $USER_ID %F{green}%3~:%f $python_info[virtualenv]
$TIME_DISPLAY%f ${vcs_info_msg_0_}%B%F{blue}%# %f%b'

  RPROMPT='${editor_info[keymap]}%(?::%F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

function set_vpn_status() {
  local tunnel_up=`/sbin/ifconfig -a | /usr/bin/egrep '^tun0' 2> /dev/null | /usr/bin/wc -l | /usr/bin/tr -d ' '`
  if [[ $tunnel_up == 1 ]]; then
      export WORK_VPN=1
      export VPN_PROMPT='%K{blue}%F{black} VPN %f%k'
  else
      export WORK_VPN=0
      export VPN_PROMPT=""
  fi
}

prompt_johno_setup "$@"

